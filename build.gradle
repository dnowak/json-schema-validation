import org.gradle.api.tasks.testing.logging.TestExceptionFormat

buildscript {
    ext.kotlin_version = '1.6.21'
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version"
    id 'org.springframework.boot' version '2.6.7'
    id "org.jetbrains.kotlin.plugin.spring" version "$kotlin_version"
    id 'org.jetbrains.kotlinx.kover' version '0.5.0'
}

ext {
    kotlin_coroutines_version = '1.6.1'
}

apply plugin: 'io.spring.dependency-management'

group = 'io.github.dnowak'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.jetbrains.kotlin:kotlin-stdlib"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-reactor:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-reactive:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-jvm:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-slf4j:$kotlin_coroutines_version"


    implementation 'io.arrow-kt:arrow-core:1.1.2'

    implementation 'io.github.microutils:kotlin-logging:2.1.21'

    implementation 'org.apache.commons:commons-lang3:3.12.0'

    implementation('org.springframework.boot:spring-boot-starter-webflux') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlin_coroutines_version"
    testImplementation 'io.kotest:kotest-runner-junit5:5.3.0'
    testImplementation group: 'io.kotest.extensions', name: 'kotest-assertions-arrow', version: '1.2.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'io.mockk:mockk:1.12.3'
}

test {
    useJUnit()
}

compileKotlin {
    kotlinOptions.jvmTarget = '17'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '17'
}

tasks.test {
    kover {
        enabled = true
        includes = ['io.github.dnowak.*']
        excludes = ['*.config.*']
    }
}

tasks.koverHtmlReport {
    enabled = true                          // false to disable report generation
}

tasks.koverXmlReport {
    enabled = true                          // false to disable report generation
}

test {
    useJUnitPlatform {
        includeEngines 'kotest', 'junit-jupiter'
    }
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true
    }
}

kover {
    disabled = false
    coverageEngine.set(kotlinx.kover.api.CoverageEngine.INTELLIJ) // change instrumentation agent and reporter
    intellijEngineVersion.set('1.0.656')
    generateReportOnCheck = true         // false to do not execute `koverReport` task before `check` task
}

tasks.koverVerify {
    rule {
        name = "Minimal line coverage rate in percents"
        bound {
            minValue = 80
        }
    }
}
